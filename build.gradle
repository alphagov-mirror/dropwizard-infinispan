plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'java-library'
}

apply plugin: 'maven-publish'

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        mavenCentral()
    } else {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
}

def dependencyVersions = [
        dropwizard: "1.3.17"
]

dependencies {
    testImplementation "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
            "org.mockito:mockito-core:3.2.0"

    implementation "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-configuration:$dependencyVersions.dropwizard"

    api 'org.infinispan:infinispan-core:7.1.1.Final'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'uk.gov.ida'
            artifactId 'dropwizard-infinispan'
            version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "${System.env.ARTIUSER}"
                password "${System.env.ARTIPASSWORD}"
            }
            url "https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/libs-release-local"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven-test'
        name = 'dropwizard-infinispan'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/dropwizard-infinispan.git'
        version {
            name = "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
        }
    }
}

defaultTasks 'clean', 'publishToMavenLocal'
