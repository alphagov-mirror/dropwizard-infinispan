plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

def dependencyVersions = [
        dropwizard:"1.1.4"
]

dependencies {
    testCompile "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
            "org.mockito:mockito-core:1.9.5"


    compile "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-configuration:$dependencyVersions.dropwizard",
            'org.infinispan:infinispan-core:7.1.1.Final'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'uk.gov.ida'
            artifactId 'dropwizard-infinispan'
            version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file:/srv/maven" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'dropwizard-infinispan'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/dropwizard-infinispan.git'
        version {
            name = "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

defaultTasks 'clean', 'publishToMavenLocal'
